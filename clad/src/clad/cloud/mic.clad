#include "clad/cloud/common.clad"

namespace Anki {
namespace Vector {
namespace CloudMic {

enum uint_8 StreamType {
	Normal,
	Blackjack,
	KnowledgeGraph
}

enum uint_8 ErrorType {
	Server,
	Timeout,
	Json,
	InvalidConfig,
	Connecting,
	NewStream,
	Token,
	TLS, // only used by connection check
	Connectivity
}

// Note: Keep this enum in sync with the version in robot/clad/src/clad/robotInterface/messageFromAnimProcess.clad
enum uint_8 ConnectionCode {
	Available,
	Connectivity,
	Tls,
	Auth,
	Bandwidth
}

structure StreamOpen {
	string session
}

structure Hotword {
	StreamType mode,
	string locale,
	string timezone,
	bool noLogging,
}

structure Filename {
	string file
}

structure AudioData {
	int_16 data[uint_16]
}

structure IntentResult {
	string intent,
	string[uint_16] parameters,
	string[uint_16] metadata
}

structure IntentError {
	ErrorType error,
	string extra
}

structure ConnectionResult {
	ConnectionCode code,
	string status,
	uint_8 numPackets,
	uint_8 expectedPackets,
}

union dupes_allowed Message {
	// anim -> cloud
	Hotword hotword,
	AudioData audio,
	Void audioDone,
	Void connectionCheck,

	// cloud -> anim
	Void stopSignal,
	Void testStarted,
	Void streamTimeout,
	ConnectionResult connectionResult,

	// cloud -> engine
	Filename debugFile,
	IntentResult result,
	IntentError error,
	StreamOpen streamOpen
}


}
}
}
